#!/usr/bin/osascript

(*
 This script will create/replace a playlist with the tracks whose
 filesystem paths match those that are passed as arguments

 The name for the playlist is defined by the first argument, all
 further arguments are filenames of the tracks
*)

on run argv

  if length of argv > 1
    
    tell application "Music"
      set targetPlaylistName to item 1 of argv

      try
        set targetPlaylist to user playlist targetPlaylistName
        delete tracks of targetPlaylist
      on error 
        set targetPlaylist to (make new user playlist with properties { name:  targetPlaylistName })
      end try

      repeat with idx from 2 to length of argv
          set trackRef to add (POSIX file (item idx of argv))
          duplicate (trackRef) to targetPlaylist
      end repeat

      play targetPlaylist
    end tell
  end if

end run
